allprojects {
    version = '2.0.0-beta.3'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    javadoc {
        options.tags("apiNote:a:API Note:")
        options.tags("implSpec:a:Implementation Requirements:")
        options.tags("implNote:a:Implementation Note:")
        options.tags("param")
        options.tags("return")
        options.tags("throws")
        options.tags("since")
        options.tags("version")
        options.tags("serialData")
        options.tags("see")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        archiveClassifier = 'javadoc'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

wrapper {
    gradleVersion = "5.6"
    distributionType = Wrapper.DistributionType.ALL
}
